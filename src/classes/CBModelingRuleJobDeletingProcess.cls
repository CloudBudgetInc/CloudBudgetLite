/** 
Copyright (c) 2021, CloudBudget, Inc.
All rights reserved.
Redistribution and use in source and binary forms, with or without modification,
are permitted provided that the following conditions are met:
* Redistributions of source code must retain the above copyright notice,
this list of conditions and the following disclaimer.
* Redistributions in binary form must reproduce the above copyright notice,
this list of conditions and the following disclaimer in the documentation
and/or other materials provided with the distribution.
* Neither the name of the CloudBudget, Inc. nor the names of its contributors
may be used to endorse or promote products derived from this software
without specific prior written permission.
THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT,
INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED
OF THE POSSIBILITY OF SUCH DAMAGE.

 */

public with sharing class CBModelingRuleJobDeletingProcess implements CBProcessInterface {
	CBModelingRule__c modelingRule;

	/**
	 *  The method deletes old data that was generated by a modeling rule group
	 * @param modelingRuleGroupId
	 * @param param2
	 */
	public void runProcess(String modelingRuleGroupId, String param2) {
		if (modelingRuleGroupId == null) {
			throw new CBException(null, 'Missed MRG Id in CBModelingRuleJobDeletingProcess');
		}
		List<CBModelingRule__c> mrList = new CBModelingRuleSelector().getModelingRulesByGroupId(modelingRuleGroupId);
		if (mrList.size() > 0) {
			modelingRule = mrList[0]; // result record type always the same
		} else {
			return;
		}
		String filter = ' CBKey__c LIKE \'' + modelingRuleGroupId + '%\' '; // all result record keys starts from modelingRuleGroupId
		Integer numberOfResultRecords = CBModelingRuleService.getNumberOfRecords(modelingRule.ResultRecordParentType__c, filter);
		CBLogService.setLog('OTHER', 'Modeling Rule Deleting', modelingRule.Name + ' old data deleting', null);

		if (numberOfResultRecords > CBConstantsService.MODELING_RULE_RECORD_LIMIT) {
			deleteOldResultRecordsWithJobs(filter);
			CBLogService.addLogDetail('Deleting via batch', 'Deleting via batch: ' + numberOfResultRecords, null);
		} else {
			deleteOldResultRecordsWithClass(filter);
			CBLogService.addLogDetail('Deleting via batch', 'Deleting via class: ' + numberOfResultRecords, null);
		}
		CBLogService.insertLog();
	}

	////////// PRIVATE METHODS //////////////
	/**
	 * The method deletes old result parent records. Children must be deleted by a trigger
	 */
	private void deleteOldResultRecordsWithClass(String filter) {
		Schema.SObjectType sObjectType = Schema.getGlobalDescribe().get(modelingRule.ResultRecordParentType__c);
		fflib_SObjectUnitOfWork uow = new fflib_SObjectUnitOfWork(
			new List<SObjectType> { sObjectType }
		);
		uow.registerDeleted(new CBSobjectSelector(sObjectType, false).selectFieldsByNames(new List<String>{'Id'}).getSobjects(filter));
		uow.commitWork();
		CBTaskDispatcherService.runNextTask();
	}
	/**
	 * The method deletes old result parent records in a job. Children must be deleted by a trigger/
	 */
	private void deleteOldResultRecordsWithJobs(String filter) {
		Map<String, String> fPMap = new Map<String, String>{
				'custom' => filter
		};
		CBDeleteBatch dj = new CBDeleteBatch(modelingRule.ResultRecordParentType__c, fPMap);
		Id batchId = Database.executeBatch(dj, 2000);
	}
	////////// PRIVATE METHODS //////////////
}